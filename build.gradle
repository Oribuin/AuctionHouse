plugins {
    id "com.github.johnrengelman.shadow" version "7.1.0" apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    group "xyz.oribuin"
    version "1.0"

    targetCompatibility = JavaVersion.VERSION_16
    sourceCompatibility = JavaVersion.VERSION_16

    repositories {
        mavenCentral()
        mavenLocal()

        maven { url 'https://libraries.minecraft.net' }
        maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
        maven { url "https://repo.rosewooddev.io/repository/public/" }
        maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
        maven { url "https://repo.codemc.org/repository/maven-public/" }
        maven { url "https://maven.enginehub.org/repo/" }
        maven { url 'https://jitpack.io' }
        maven { url "https://repo.mattstudios.me/artifactory/public/" }
    }
}

dependencies {
    implementation project(':Plugin')
    implementation project(':NMS:Wrapper')
    implementation project(path: ':NMS:v1_17_R1', configuration: 'remapped')
    implementation project(path: ':NMS:v1_18_R1', configuration: 'remapped')
    implementation project(path: ':NMS:v1_18_R2', configuration: 'remapped')
}

shadowJar {
    archiveClassifier.set(null)
    relocate('dev.triumphteam.gui', "${project.group}.auctionhouse.libs.gui")
    relocate('dev.rosewood.rosegarden', "${project.group}.auctionhouse.libs.rosegarden")
    relocate('net.kyori', "${project.group}.auctionhouse.libs.kyori")

}

tasks.getByName("build").dependsOn("shadowJar")